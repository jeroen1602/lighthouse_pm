plugins {
    id "com.android.application"
    id "kotlin-android"
    // The Flutter Gradle Plugin must be applied after the Android and Kotlin Gradle plugins.
    id "dev.flutter.flutter-gradle-plugin"
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def home = System.getProperty('user.home')
def keystoreProperties = new Properties()
def keystorePropertiesFile = new File(home + '/android-keys/key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}
if (keystoreProperties['storeFile'] && keystoreProperties['storeFile'].contains('~')) {
    keystoreProperties['storeFile'] = keystoreProperties['storeFile'].replace('~', home)
}

android {
    namespace 'com.jeroen1602.lighthouse_pm'
    compileSdkVersion Math.max(35, flutter.compileSdkVersion)
    ndkVersion "27.2.12479018" // TODO use this: flutter.ndkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        googlePlay {
            java.srcDirs += 'src/googlePlay/kotlin'
        }
        defaultVersion {
            java.srcDirs += 'src/defaultVersion/kotlin'
        }
    }

    defaultConfig {
        applicationId "com.jeroen1602.lighthouse_pm"
        minSdkVersion Math.max(21, flutter.minSdkVersion)
        targetSdkVersion Math.max(35, flutter.compileSdkVersion)
        versionCode flutter.versionCode
        versionName flutter.versionName
    }
    signingConfigs {
        release {
            if (!keystoreProperties['keyAlias']) {
                println "WARNING: Release keystore has not been found! Building release builds will be impossible!"
                println "Keystore properties should be located at: `${keystorePropertiesFile}`."
                println "Go to `android/app/build.gradle` to change this if needed!"
            } else {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
                storePassword keystoreProperties['storePassword']
            }
        }
    }

    buildFeatures {
        buildConfig true
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            ndk {
                abiFilters "armeabi-v7a", "arm64-v8a", "x86_64"
            }
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions 'productFlavors'
    productFlavors {
        googlePlay {
            dimension 'productFlavors'
        }
        defaultVersion {
            dimension 'productFlavors'
        }
    }
    lint {
        //TODO: check to see if this can be removed in future versions, since a lint for release builds is useful
        checkReleaseBuilds true
        disable 'InvalidPackage'
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.9.25"

    // For now only needed for googlePlayImplementation
    googlePlayImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.9.0"

    def billing_version = "7.1.1"

    googlePlayImplementation "com.android.billingclient:billing:$billing_version"
    googlePlayImplementation "com.android.billingclient:billing-ktx:$billing_version"

}
